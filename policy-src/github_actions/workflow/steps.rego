# FILE_NAME: steps.rego
# DESCRIPTION: Workflow step-level policies (CKV_GHA + CKV2_SPVS supply chain).
# VERSION: 1.3.0
# AUTHORS: DevOps Team

package workflow

import rego.v1

import data.lib

# CKV_GHA_1: ACTIONS_ALLOW_UNSECURE_COMMANDS on job or step env
deny contains msg if {
	some job_name
	job := input.jobs[job_name]
	is_object(job.env)
	lib.env_truthy(job.env.ACTIONS_ALLOW_UNSECURE_COMMANDS)
	lib.policy_active("CKV_GHA_1", skip_scopes_for_job(job_name))
	msg := sprintf("CKV_GHA_1 [jobs.%s.env]: job %s must not set ACTIONS_ALLOW_UNSECURE_COMMANDS", [job_name, job_name])
}

deny contains msg if {
	entry := all_steps[_]
	is_object(entry.step.env)
	lib.env_truthy(entry.step.env.ACTIONS_ALLOW_UNSECURE_COMMANDS)
	lib.policy_active("CKV_GHA_1", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV_GHA_1 [%s.env]: step in job %s must not set ACTIONS_ALLOW_UNSECURE_COMMANDS", [step_yaml_path(entry), entry.job])
}

# CKV_GHA_2: shell injection in run
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	run != ""
	some pattern in lib.shell_injection_patterns
	regex.match(pattern, run)
	lib.policy_active("CKV_GHA_2", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV_GHA_2 [%s.run]: job %s run block may be vulnerable to shell injection", [step_yaml_path(entry), entry.job])
}

# CKV_GHA_3: curl with secrets on same line
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	run != ""
	some line in split(run, "\n")
	contains(line, "curl")
	contains(line, "secret")
	lib.policy_active("CKV_GHA_3", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV_GHA_3 [%s.run]: job %s suspicious curl with secrets in run block", [step_yaml_path(entry), entry.job])
}

# CKV_GHA_4: netcat reverse shell pattern
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	regex.match(`(nc|netcat) ([0-9]{1,3})\.([0-9]{1,3})\.([0-9]{1,3})\.([0-9]{1,3})`, run)
	lib.policy_active("CKV_GHA_4", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV_GHA_4 [%s.run]: job %s suspicious netcat with IP in run block", [step_yaml_path(entry), entry.job])
}

# CKV2_SPVS_2: set -euo pipefail
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	run != ""
	not regex.match(`[\s\S]*set\s+-euo\s+pipefail`, run)
	lib.policy_active("CKV2_SPVS_2", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_2 [%s.run]: job %s run block must include set -euo pipefail", [step_yaml_path(entry), entry.job])
}

# CKV2_SPVS_3: no xtrace
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	run != ""
	regex.match(`[\s\S]*(set\s+-x|set\s+-o\s+xtrace|\bxtrace\b)`, run)
	lib.policy_active("CKV2_SPVS_3", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_3 [%s.run]: job %s run block must not enable xtrace", [step_yaml_path(entry), entry.job])
}

# CKV2_SPVS_4: python unbuffered
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	run != ""
	regex.match(`[\s\S]*\bpython3?\b`, run)
	not regex.match(`[\s\S]*\bpython3?\s+(-\S+\s+)*-u\b`, run)
	not lib.python_unbuffered_env(entry.step)
	lib.policy_active("CKV2_SPVS_4", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_4 [%s.run]: job %s python invocation must use -u or PYTHONUNBUFFERED", [step_yaml_path(entry), entry.job])
}

# CKV2_SPVS_5: pin actions
deny contains msg if {
	entry := all_steps[_]
	uses := lib.step_uses(entry.step)
	uses != ""
	not lib.uses_allowed(uses)
	lib.policy_active("CKV2_SPVS_5", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_5 [%s.uses]: job %s uses %s must use SHA, ./, docker://, or internal /actions/ tag", [step_yaml_path(entry), entry.job, uses])
}

# CKV2_SPVS_5B: no parent path uses
deny contains msg if {
	entry := all_steps[_]
	uses := lib.step_uses(entry.step)
	startswith(uses, "../")
	lib.policy_active("CKV2_SPVS_5B", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_5B [%s.uses]: job %s uses %s must not use ../ local action refs", [step_yaml_path(entry), entry.job, uses])
}

# CKV2_SPVS_6: inputs in run (GHA expressions or mistaken shell-style inputs.* refs)
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	lib.inputs_in_run(run)
	lib.policy_active("CKV2_SPVS_6", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_6 [%s.run]: job %s must map inputs to env, not interpolate in run", [step_yaml_path(entry), entry.job])
}

# CKV2_SPVS_7: static cloud credentials in env
deny contains msg if {
	entry := all_steps[_]
	is_object(entry.step.env)
	some key in lib.forbidden_env_keys
	entry.step.env[key]
	lib.policy_active("CKV2_SPVS_7", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_7 [%s.env]: job %s must not set static credential env %s", [step_yaml_path(entry), entry.job, key])
}

# CKV2_SPVS_13: curl pipe bash
deny contains msg if {
	entry := all_steps[_]
	run := lib.step_run(entry.step)
	lib.curl_pipe_shell(run)
	lib.policy_active("CKV2_SPVS_13", skip_scopes_for_step(entry.job, entry.step))
	msg := sprintf("CKV2_SPVS_13 [%s.run]: job %s must not pipe curl/wget into shell", [step_yaml_path(entry), entry.job])
}
